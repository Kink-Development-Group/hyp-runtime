// HypnoScript test file for modern language features
// Focus: embed, pendulum, murmur, await, lucidFallback, dreamReach optional chaining

Focus {
    observe "=== New language features in action ===";

    // embed stores a global memory
    embed deepMemory: number = 42;
    observe "Embedded value: " + deepMemory;

    // Pendulum (HypnoScript-style for-loop) with murmur output
    observe "=== Pendulum summary ===";
    induce sum: number = 0;
    pendulum (induce i: number = 0; i < 4; i = i + 1) {
        sum = sum + i;
        murmur "Iteration " + i + ": sum = " + sum;
    }
    observe "Final sum: " + sum;

    // Nullish operator (lucidFallback)
    induce optionalLocale = GetEnv("HYP_RUNTIME_DEMO_LOCALE");
    induce resolvedLocale = optionalLocale lucidFallback "de-DE";
    observe "Locale fallback: " + resolvedLocale;

    // await can also be applied to simple expressions
    induce awaitedValue = await (sum + 5);
    observe "Await result: " + awaitedValue;

    // Optional Chaining mit dreamReach
    session DataEnvelope {
        expose payload;

        suggestion attach(value: string) {
            this.payload = value;
        }
    }

    induce emptyEnvelope = DataEnvelope();
    induce fullEnvelope = DataEnvelope();
    fullEnvelope.attach("Trance Transcript");

    observe "Optional chaining empty: " + ((emptyEnvelope dreamReach payload) lucidFallback "(empty)");
    observe "Optional chaining filled: " + ((fullEnvelope dreamReach payload) lucidFallback "(empty)");

    // murmur as a subtle diagnostic output
    murmur "DreamReach returns null when nothing is present.";

    // Multiple output channels remain combinable
    observe "Standard output active.";
    whisper "soft whisper"
    command "COMMAND CHANNEL";

    finale {
        observe "";
        observe "=== New language features demonstrated successfully ===";
    }
}

Relax
